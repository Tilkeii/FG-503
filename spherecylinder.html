<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<title>WebGL - Sph&egrave;res, cylindres et mod&egrave;le de Phong</title>




<script type="x-shader/x-vertex" id="vshader">
     uniform mat4 projection;
     uniform mat4 modelview;
     uniform vec4 lightPosition;
     uniform mat3 normalMatrix;

     attribute vec3 vcoords;
     attribute vec3 vnormal;
     attribute vec2 vtexcoord;

     varying vec3 fN, fL, fE;
     varying vec3 fviewCoords;
     varying vec2 fTexCoord;

     void main() {
        vec4 eyeCoords = modelview * vec4(vcoords,1.0);
        gl_Position = projection * eyeCoords;

        vec3 pos = eyeCoords.xyz;

          // Modify light position according to the 
          // transformations applied if you want the light
          // to move with the objects
//        vec3 light = (modelview * lightPosition).xyz;
        vec3 light = (lightPosition).xyz;

          // Now compute vectors L, E and N.
          // Note that N is affected by the 
          // transformations applied.

        fL = normalize( light - pos );
        fE = normalize(-pos);
        fN = normalMatrix * normalize(vnormal);

  
        fviewCoords = eyeCoords.xyz;
        fTexCoord = vtexcoord;
     }
</script>

<script type="x-shader/x-fragment" id="fshader">
     precision mediump float;

     uniform vec4 ambientProduct;
     uniform vec4 diffuseProduct;
     uniform vec4 specularProduct;
     uniform float shininess;
     uniform bool phong;
     uniform bool activateTex;
     uniform bool translucide;

     varying vec3 fN, fL, fE;
     varying vec3 fviewCoords;
     varying vec2 fTexCoord;

    uniform sampler2D texture;
    uniform float alpha;

     void main() {

          vec4 Color;
    
          vec4 ambient = ambientProduct;

          if(phong){
          
            vec3 H = normalize( fL + fE );
            
            float factorLN = max( dot(fL, fN), 0.0 );
            vec4  diffuse = factorLN*diffuseProduct;

            float factorNH = pow( max(dot(fN, H), 0.0), shininess );
            vec4  specular = factorNH * specularProduct;

            if( dot(fL, fN) < 0.0 )
                specular = vec4(0.0, 0.0, 0.0, 1.0);

            Color = ambient + diffuse + specular;
            Color.a = 1.0;
          }
          else {
            Color = ambient;
          }

          if(translucide){
            gl_FragColor = Color * texture2D(texture, fTexCoord) * alpha;
          }
          else if(activateTex){
            gl_FragColor = Color * texture2D(texture, fTexCoord);
          }
          else {
            gl_FragColor = Color;
          }
     }
</script>

<script type="x-shader/x-vertex" id="vshaderbox">
     uniform mat4 projection;
     uniform mat4 modelview;

     attribute vec3 vcoords;
     attribute vec3 vnormal;
     attribute vec2 vtexcoord;

     varying vec3 fCoords;

     void main() {
        vec2 tmp = vtexcoord;
        vec3 tmp2 = vnormal;
   // inserted only to map texcoord and normal in this shader
   // (required since models uses texture coordinates even though
   //   we will not use them in this shader)

        vec4 eyeCoords = modelview * vec4(vcoords,1.0);
        gl_Position = projection * eyeCoords;
        fCoords = vcoords;//gl_Position.xyz;
     }
</script>

<script type="x-shader/x-fragment" id="fshaderbox">
     precision mediump float;

     varying vec3 fCoords;

     uniform samplerCube skybox;

     void main() {
          gl_FragColor = textureCube(skybox, fCoords);
     }
</script>


<script type="text/javascript" src="Common/simple-rotator-with-compensated-earth-rotation.js"></script>
<script type="text/javascript" src="basic-objects-IFS.js"></script>
<script type="text/javascript" src="Common/MV.js"></script>
<script type="text/javascript" src="Common/webgl-utils.js"></script>
<script type="text/javascript" src="spherecylinder.js"></script>

</head>
<body style="background-color:#DDD">

<h1>WebGL - Sph&egrave;res, cylindres et mod&egrave;le de Phong</h1>

<h2>Utilisez la souris pour faire tourner les objets &agrave; l'aide de SimpleRotator()</h2>
<noscript><hr><h3>This page requires Javascript and a web browser that supports WebGL</h3><hr></noscript>


<div>

    <canvas width=600 height=600 id="glcanvas" style="background-color:blue"></canvas>

</div>


</body>
</html>